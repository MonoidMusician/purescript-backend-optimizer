module PureScript.Backend.Optimizer.Builder
  ( BuildEnv
  , BuildOptions
  , BuildState
  , buildModules
  , trimIncrementalState
  ) where

import Prelude

import Data.FunctorWithIndex (mapWithIndex)
import Data.List (List, foldM)
import Data.List as List
import Data.Map (Map)
import Data.Map as Map
import Data.Maybe (Maybe(..))
import Data.Set (Set)
import Data.Set as Set
import Data.Tuple (Tuple(..), fst)
import PureScript.Backend.Optimizer.Analysis (BackendAnalysis)
import PureScript.Backend.Optimizer.Convert (BackendModule, OptimizationSteps, toBackendModule)
import PureScript.Backend.Optimizer.CoreFn (Ann, Ident, Import(..), Module(..), ModuleName, Qualified, isPrimModule)
import PureScript.Backend.Optimizer.QIMap (QIMap)
import PureScript.Backend.Optimizer.QIMap as QIMap
import PureScript.Backend.Optimizer.Semantics (BackendExpr, Ctx, ExternImpl, InlineDirectiveMap, noDirectives, unionDirectives)
import PureScript.Backend.Optimizer.Semantics.Foreign (ForeignEval)
import PureScript.Backend.Optimizer.Syntax (BackendSyntax)

type BuildEnv =
  { implementations :: QIMap (Tuple BackendAnalysis ExternImpl)
  , directives :: InlineDirectiveMap
  , built :: Set ModuleName
  , moduleCount :: Int
  , moduleIndex :: Int
  }
type BuildState =
  { built :: Set ModuleName
  , directives :: InlineDirectiveMap
  , implementations :: QIMap (Tuple BackendAnalysis ExternImpl)
  }

type BuildOptions m =
  { analyzeCustom :: Ctx -> BackendSyntax BackendExpr -> Maybe BackendAnalysis
  , directives :: InlineDirectiveMap
  , foreignSemantics :: Map (Qualified Ident) ForeignEval
  , onPrepareModule :: BuildEnv -> Module Ann -> m (Module Ann)
  , onCodegenModule :: BuildEnv -> Module Ann -> BackendModule -> OptimizationSteps -> m Unit
  , traceIdents :: Set (Qualified Ident)
  -- Restore a saved `BuildState`, where `built` is the list of modules that
  -- do not need to be rebuilt (as long as their dependencies are also built),
  -- and `directives` and `implementations` may be from the whole state or
  -- already filtered to those modules
  , incremental :: Maybe BuildState
  }

-- | Builds modules given a _sorted_ list of modules.
-- | See `PureScript.Backend.Optimizer.CoreFn.Sort.sortModules`.
buildModules :: forall m. Monad m => BuildOptions m -> List (Module Ann) -> m BuildState
buildModules options coreFnModulesUnfiltered =
  foldM go state0 coreFnModules
  where
  foreignSemantics = QIMap.fromMap options.foreignSemantics
  state0 = case options.incremental of
    Nothing -> { directives: options.directives, implementations: QIMap.empty, built: Set.empty }
    Just incrementalState ->
      let r = trimIncrementalState coreFnModulesUnfiltered incrementalState
      -- These directives are only module exports, but we also need to include
      -- the global directives first
      in r { directives = unionDirectives options.directives r.directives }
  coreFnModules = mapWithIndex Tuple $ coreFnModulesUnfiltered
    # List.filter \(Module { name }) -> not $ Set.member name state0.built
  moduleCount = List.length coreFnModules
  go { directives, implementations, built } (Tuple moduleIndex coreFnModule) = do
    let buildEnv = { built, implementations, directives, moduleCount, moduleIndex }
    coreFnModule'@(Module { name }) <- options.onPrepareModule buildEnv coreFnModule
    let
      Tuple optimizationSteps backendMod = toBackendModule coreFnModule'
        { analyzeCustom: options.analyzeCustom
        , currentModule: name
        , currentLevel: 0
        , toLevel: Map.empty
        , implementations
        , moduleImplementations: QIMap.empty
        , directives
        , dataTypes: Map.empty
        , foreignSemantics
        , rewriteLimit: 10_000
        , traceIdents: options.traceIdents
        , optimizationSteps: []
        }
      newImplementations = QIMap.union implementations backendMod.implementations
    options.onCodegenModule (buildEnv { implementations = newImplementations }) coreFnModule' backendMod optimizationSteps
    pure
      { directives: unionDirectives backendMod.directives directives
      , implementations: newImplementations
      , built: Set.insert name built
      }

trimIncrementalState :: List (Module Ann) -> BuildState -> BuildState
trimIncrementalState _ { built } | Set.isEmpty built =
  { built: Set.empty, directives: noDirectives, implementations: QIMap.empty }
trimIncrementalState allModules toVerify = { built, directives, implementations }
  where
  depMap = Map.fromFoldable $ allModules <#> \(Module { name, imports }) ->
    Tuple name (Set.filter (not isPrimModule) $ Set.fromFoldable $ imports <#> \(Import _ dep) -> dep)
  -- Trim the `built` set down to modules that are transitively built
  built = trimming $ toVerify.built
  -- Filter the module-export directives down to modules included in `built`
  directives = Tuple (QIMap.matchModules built (fst toVerify.directives)) Map.empty
  -- Filter the identifier implementations down to modules included in `built`
  implementations = toVerify.implementations # QIMap.matchModules built

  trim moduleSet = moduleSet # Set.filter \name ->
    case Map.lookup name depMap of
      Nothing -> true -- Just assume it is okay, if dependency information was not included
      Just deps -> Set.subset deps moduleSet
  trimming moduleSet =
    case trim moduleSet of
      reduced | reduced == moduleSet -> moduleSet
      reduced -> trimming reduced
